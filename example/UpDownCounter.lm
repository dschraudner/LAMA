-- Counts periodically up and down ranging from 0 to 10.
-- The example can be found at
-- http://www-verimag.imag.fr/Language-Design,112.html?artpage=7-8 .
-- The original version can be found in the paper about mode automata
-- (http://www-verimag.imag.fr/~maraninx/spip.php?article102).
-- Notice that x is initialised with -1 instead of 0 here. With that
-- xo is 0 at the beginning and not 1.

nodes
  node UpDown () returns (xo : int)
  let
    local
      x_ : int;
    state
      x : int;
    definition
      xo = x_;
    transition
      x' = x_;

    automaton let
      location A let
        definition x_ = (+ x 1);
      tel
      location B let
        definition x_ = (- x 1);
      tel
      initial A;
      edge (A, B) : (= x 10);
      edge (B, A) : (= x 0);
      edge (A, A) : (not (= x 10));
      edge (B, B) : (not (= x 0));
    tel

    initial x = (- 1);
  tel
local x : int;
state x_1 : int;
definition x = (use UpDown);
transition x_1' = x;
initial x_1 = (- 1);

invariant
  (and
    (and (>= x 0) (<= x 10)) -- ranges only from 0 to 10
    (or (= (- x_1 x) 1) (= (- x_1 x) (- 1))) -- always moves just one step
  );
